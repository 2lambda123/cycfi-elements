###############################################################################
#  Copyright (c) 2016-2019 Joel de Guzman
#
#  Distributed under the MIT License (https://opensource.org/licenses/MIT)
###############################################################################

set(CMAKE_CXX_STANDARD 14)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

project(${app_name})

###############################################################################
# Sources (and Resources)

if (${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
   file(
      GLOB_RECURSE PHOTON_RESOURCES
      ${CMAKE_CURRENT_SOURCE_DIR}/../resources/fonts/*.ttf
      ${CMAKE_CURRENT_SOURCE_DIR}/../resources/macos/fonts/*.ttf
   )
elseif (${CMAKE_SYSTEM_NAME} MATCHES "Linux")
   file(
      GLOB_RECURSE PHOTON_RESOURCES
      ${CMAKE_CURRENT_SOURCE_DIR}/../resources/fonts/*.ttf
   )
endif()

if (${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
   file(
      GLOB_RECURSE APP_RESOURCES
      ${CMAKE_CURRENT_SOURCE_DIR}/resources/*.ttf
      ${CMAKE_CURRENT_SOURCE_DIR}/resources/*.jpeg
      ${CMAKE_CURRENT_SOURCE_DIR}/resources/*.jpg
      ${CMAKE_CURRENT_SOURCE_DIR}/resources/*.png
   )
elseif (${CMAKE_SYSTEM_NAME} MATCHES "Linux")
   file(
      GLOB_RECURSE APP_RESOURCES
      ${CMAKE_CURRENT_SOURCE_DIR}/resources/*.ttf
      ${CMAKE_CURRENT_SOURCE_DIR}/resources/*.jpeg
      ${CMAKE_CURRENT_SOURCE_DIR}/resources/*.jpg
      ${CMAKE_CURRENT_SOURCE_DIR}/resources/*.png
      ${CMAKE_CURRENT_SOURCE_DIR}/resources/*.json
   )
endif()

source_group(Resources
   FILES
   ${PHOTON_RESOURCES}
   ${APP_RESOURCES}
)

if (${CMAKE_SYSTEM_NAME} MATCHES "Linux")
   file(
      COPY ${PHOTON_RESOURCES} ${APP_RESOURCES}
      DESTINATION ${CMAKE_CURRENT_BINARY_DIR}
   )
endif()

###############################################################################
# The executable App

if (${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
   add_executable(
      ${app_name}
      MACOSX_BUNDLE
      main.cpp
      ${PHOTON_RESOURCES}
      ${APP_RESOURCES}
   )
else()
   add_executable(
      ${app_name}
      main.cpp
      ${PHOTON_RESOURCES}
      ${APP_RESOURCES}
   )
endif()

###############################################################################
# Libraries and linking

target_link_libraries(${app_name}
   libphoton
)

if (${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
   set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -framework AppKit")
endif()

###############################################################################
# Resource file properties

if (${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
   set(MACOSX_BUNDLE_BUNDLE_NAME ${app_name})
   set(MACOSX_BUNDLE_COPYRIGHT ${app_copyright})
   set(MACOSX_BUNDLE_GUI_IDENTIFIER ${app_id})
   set(MACOSX_BUNDLE_SHORT_VERSION_STRING ${app_version})
endif()

if (${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
   set_source_files_properties(
      ${PHOTON_RESOURCES}
      ${APP_RESOURCES}
      PROPERTIES
      MACOSX_PACKAGE_LOCATION Resources
   )

   set_target_properties(
      ${app_name}
      PROPERTIES
      MACOSX_BUNDLE_INFO_PLIST ${CMAKE_CURRENT_SOURCE_DIR}/../resources/macos/plist.in
   )
endif()

